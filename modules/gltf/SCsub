#!/usr/bin/env python

import os

Import("env")
Import("env_modules")

# For MP3 support in the GLTFDocumentExtensionAudio.
if env["module_minimp3_enabled"]:
    thirdparty_dir = "#thirdparty/minimp3/"
    if not env.msvc:
        env_modules.Append(CPPFLAGS=["-isystem", Dir(thirdparty_dir).path])
    else:
        env_modules.Prepend(CPPPATH=[thirdparty_dir])
    if not env["minimp3_extra_formats"]:
        env_modules.Append(CPPDEFINES=["MINIMP3_ONLY_MP3"])

# For OGG Vorbis support in the GLTFDocumentExtensionAudio.
if env["module_vorbis_enabled"]:
    thirdparty_dir = "#thirdparty/libvorbis/"
    env_modules.Prepend(CPPPATH=[thirdparty_dir])
    if env["builtin_libogg"]:
        env_modules.Prepend(CPPPATH=["#thirdparty/libogg"])

# Inter-module compile dependency: Include Jolt from GLTF.
# This allows GLTF in The Mirror's engine fork to use our Jolt implementation.
jolt_module_path = os.path.abspath(os.path.join(os.getcwd(), "..", "jolt"))
env_modules.Prepend(CPPPATH=[os.path.join(jolt_module_path, "thirdparty", "JoltPhysics")])

env_gltf = env_modules.Clone()

# Godot source files

env_gltf.add_source_files(env.modules_sources, "*.cpp")
env_gltf.add_source_files(env.modules_sources, "structures/*.cpp")

SConscript("extensions/SCsub")

if env.editor_build:
    env_gltf.add_source_files(env.modules_sources, "editor/*.cpp")
